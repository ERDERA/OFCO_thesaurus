# count by main disability categories

PREFIX ofco: <https://w3id.org/ofco#>
PREFIX ofcov: <https://w3id.org/ofco/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?topCategory ?categoryLabel (COUNT(DISTINCT ?disorder) as ?diseaseCount)
WHERE {
  ?disorder ofcov:hasDisabilityAnnotation ?annotation .
  ?annotation a ofcov:DisabilityDisorderAssociation ;
              ofcov:concernsDisability ?disability .
  
  # get up to main categories (direct subclasses of ActivitiesAndParticipation)
  ?disability rdfs:subClassOf+ ?topCategory .
  ?topCategory rdfs:subClassOf ofco:ActivitiesAndParticipation .
  
  # exclude main class
  FILTER(?topCategory != ofco:ActivitiesAndParticipation)
  
  OPTIONAL { ?topCategory rdfs:label ?categoryLabel . FILTER(lang(?categoryLabel) = "en") }
}
GROUP BY ?topCategory ?categoryLabel
ORDER BY DESC(?diseaseCount)